program = _{ SOI ~ NEWLINE* ~ (stmt ~ NEWLINE+) * ~ stmt? ~ EOI }
stmt = {
	assgn_stmt
    | pattern_slot 
    | track
    | mixer
}
assgn_stmt = { pattern_assgmt }


pattern_assgmt = { pattern_slot ~ (pattern | pattern_list) }
pattern_slot = @{ track ~ "." ~ unsigned_int }
track = @{ mixer ~ unsigned_int }
mixer = { "$" }


pattern_list = { pattern ~ ("," ~ NEWLINE* ~ pattern)+ }
pattern = { "|" ~ (chord | event | parenthesised_event)+ ~  "|" }
parenthesised_event = { "(" ~ (event | chord | parenthesised_event)+  ~ ")" ~ event_method* }
chord = { "[" ~ (event | parenthesised_event)+ ~ "]" }
event = @{ octave | alteration | note ~ event_method* ~ note* | event_method }
event_method = { "_" | "." | "*" | ":" }
note = { ASCII_DIGIT | 'a'..'f' | "r" }
octave = { "o" | "O" }
alteration = { "+" | "-" }


number = @{ "-"? ~ unsigned_float }
unsigned_float = @{ unsigned_int
    ~ ("." ~ ASCII_DIGIT*)?
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}
unsigned_int = @{ "0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
WHITESPACE = _{ " " | "\t" }
COMMENT = _{ "#" ~ (!"\n" ~ ANY)* }

