File = _{ SOI ~ NEWLINE* ~ (Statement ~ NEWLINE+)* ~ Statement? ~ EOI }
Statement = {
	AssignStatement
    | SuperExpression
}
AssignStatement = { PatternAssignment | VariableAssignment | PropertiesAssignment }

VariableAssignment = { Variable ~ "=" ~ SuperExpression }
PropertiesAssignment = { SuperExpression ~ Properties }
SuperExpression = {  MethodCall | Expression }
MethodCall = { Expression ~ FunctionExpression+ }
Expression = { 
    PropertyGetter
    | Boolean
    | FunctionExpression
    | Identifier 
    | Variable 
    | Pattern
    | Number 
    | String 
    | PatternSlot 
    | Track 
    | Mixer
    | Properties
    | Array }
FunctionExpression = { FunctionCall | FunctionListCall }
FunctionListCall = { "[" ~ NEWLINE* ~ FunctionExpression ~ NEWLINE* ~ ("," ~ NEWLINE* ~ FunctionExpression ~ NEWLINE*)* ~ "]" }
FunctionCall = { Identifier | "(" ~ NEWLINE* ~ Identifier ~ NEWLINE* ~ (Expression ~ NEWLINE*)+ ~ ")" }
Boolean = { "true" | "false" }
PatternSlot = ${ Track ~ "." ~ UnsignedInt }
Track = ${ Mixer ~ UnsignedInt }
Mixer = { "$" }
Array = { "[" ~ NEWLINE* ~ SuperExpression ~ NEWLINE* ~ ("," ~ NEWLINE* ~ SuperExpression ~ NEWLINE*)* ~ "]" }
PropertyGetter = { (Variable 
    | Pattern 
    | PatternSlot 
    | Track 
    | Mixer
    | Properties) ~ NEWLINE? ~ ("." ~ NEWLINE? ~ Identifier ~ NEWLINE?)+ }
Properties = { "{" ~ NEWLINE* ~ KvPair ~ NEWLINE* ~ ("," ~ NEWLINE* ~ KvPair)* ~ NEWLINE* ~ "}" }
KvPair = { NEWLINE* ~ Identifier ~ NEWLINE* ~ ":" ~ NEWLINE* ~ PropertyValue ~ NEWLINE* }
PropertyValue = { SuperExpression | PatternExpression }
Variable = ${ ":" ~ Identifier }
String = ${ "\"" ~ StringInner ~ "\"" }
StringInner = @{ Char* }
Char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
Identifier = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
Number = @{ "-"? ~ UnsignedFloat }
UnsignedFloat = @{ UnsignedInt
    ~ ("." ~ ASCII_DIGIT*)?
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}
UnsignedInt = @{ "0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
WHITESPACE = _{ " " | "\t" }
COMMENT = _{ "#" ~ (!"\n" ~ ANY)* }

// Patterns (meta)language

PatternAssignment = { PatternSlot ~ PatternSuperExpression }
PatternSuperExpression = { PatternExpressionList | PatternExpression }
PatternExpressionList = { PatternExpression ~ NEWLINE* ~ ("," ~ NEWLINE* ~ PatternExpression)+ }
PatternExpression = { Pattern ~ PatternInnerMethod? ~ ("=>" ~ FunctionExpression*)? ~ Properties? }
PatternInnerMethod = { FunctionExpression }
Pattern = { "|" ~ EventGroup* ~  "|" }
EventGroup = ${ Event+ }
Event = { Group | Chord | ParenthesisedEventGroup }
ParenthesisedEventGroup = ${ "(" ~ WHITESPACE* ~ (EventGroup ~ WHITESPACE*)+ ~ ")" }
Chord = ${ "[" ~ WHITESPACE* ~ (EventGroup ~ WHITESPACE*)+ ~ "]" }
Group = ${ PatternAtom+ }
PatternAtom = { 
    EventMethod 
    | Octave 
    | Alteration
    | Pitch
    | Pause
    | PatternInput
    | Modulation }
Pitch = { ASCII_DIGIT | 'a'..'f' }
Pause = { "r" }
PatternInput = { "%" }
Octave = { "o" | "O" }
Alteration = { "+" | "-" }
EventMethod = { "_" | "." | "*" | ":" }
Modulation = { ModulationSymbol | LiteralModulation }
ModulationSymbol = { "p" | "F" | "<" | ">" }
LiteralModulation = { ("{" ~ WHITESPACE* ~ Number ~ WHITESPACE* ~ "}") }